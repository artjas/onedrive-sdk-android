apply plugin: 'com.android.library'
apply plugin: 'maven'

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}


android {
    compileSdkVersion 29
    buildToolsVersion '29.0.2'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
    }

    lintOptions {
        disable 'InvalidPackage', 'AllowBackup'
    }
    buildTypes {
        debug {
            testCoverageEnabled = true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

apply plugin: 'checkstyle'

check.dependsOn 'checkstyle'

task checkstyle(type: Checkstyle) {
    configFile file("../checkstyle.xml")
    source 'src'
    include 'main/**/*.java'
    exclude '**/generated/**',
            '**/extensions/**',
            '**/core/OneDriveErrorCodes.java',
            '**/http/OneDrive*Error*.java',
            '**/authentication/ServiceInfo.java',
            '**/authentication/DiscoveryServiceResponse.java'
    ignoreFailures = false
    classpath = files()
}

dependencies {
    implementation ('com.microsoft.services.msa:msa-auth:0.8.4') {
        exclude module: 'com.microsoft.services.msa'
    }
    implementation ('com.microsoft.aad:adal:1.1.7') {
        exclude module: 'com.microsoft.aad'
    }
    implementation ('com.google.code.gson:gson:2.3.1') {
        exclude module: 'com.google.code.gson'
    }
}


task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath())
    classpath += configurations.compile
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives javadocJar
    archives sourcesJar
}
